//file:noinspection GroovyUnusedAssignment
//file:noinspection GrUnresolvedAccess
plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
}

group 'cn.apisium.nekomaid'
version '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'jitpack'
        url = 'https://www.jitpack.io'
    }
    maven {
        name = 'PlugMan'
        url = 'https://raw.githubusercontent.com/TheBlackEntity/PlugMan/repository/'
    }
    maven {
        name = 'placeholderapi'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        name = 'CodeMC'
        url = 'https://repo.codemc.org/repository/maven-public/'
    }
    maven {
        name = 'onarandombox'
        url = "https://repo.onarandombox.com/content/groups/public/"
    }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'io.netty:netty-all:4.1.68.Final'
    compileOnly 'org.jetbrains:annotations:22.0.0'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.14.1'
    compileOnly 'com.rylinaux:PlugMan:2.2.3'
    compileOnly 'com.github.Apisium:Uniporter:1.3.1-SNAPSHOT'
    compileOnly 'com.github.jikoo.OpenInv:openinvapi:2f36a4d4dc'
    compileOnly 'me.clip:placeholderapi:2.10.10'
    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.8.0'
    compileOnly 'com.github.oshi:oshi-core:5.8.2'
    compileOnly 'com.onarandombox.multiversecore:Multiverse-Core:4.3.1-SNAPSHOT'
    compileOnly ('com.github.MilkBowl:VaultAPI:1.7') {
        exclude group: 'org.bukkit'
    }
    compileOnly ('org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT') {
        exclude group: 'org.bukkit'
    }

    implementation 'com.alibaba:fastjson:1.2.78'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
    implementation 'com.github.Apisium:netty-engine.io:03931aef8c'
    implementation 'org.apache.commons:commons-compress:1.21'
    implementation 'commons-io:commons-io:2.11.0'
    implementation ('io.socket:engine.io-server:5.0.0') {
        exclude group: 'com.google.code.gson'
    }
    implementation ('com.maxmind.geoip2:geoip2:2.15.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'commons-codec'
    }
    implementation ('io.socket:socket.io-server:3.0.1') {
        exclude group: 'org.json'
        exclude group: 'com.squareup.okhttp3'
        exclude group: 'io.socket', module: 'engine.io-server'
        exclude group: 'io.socket', module: 'engine.io-client'
    }

    annotationProcessor 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

shadowJar {
    relocate('com.alibaba', 'cn.apisium.nekomaid.libs.com.alibaba')
    relocate('org.json', 'cn.apisium.nekomaid.libs.org.json')
    relocate('javax.servlet', 'cn.apisium.nekomaid.libs.javax.servlet')
    relocate('org.bstats', 'cn.apisium.nekomaid.libs.org.bstats')
    relocate('org.apache.commons.io', 'cn.apisium.nekomaid.libs.org.apache.commons.io')
    relocate('org.apache.commons.compress', 'cn.apisium.nekomaid.libs.org.apache.commons.compress')
    relocate('io.socket', 'cn.apisium.nekomaid.libs.io.socket')
    relocate('com.maxmind', 'cn.apisium.nekomaid.libs.com.maxmind')
    exclude 'io/papermc/**'

    archiveClassifier.set('')
    minimize()
}
build.dependsOn(shadowJar)
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
